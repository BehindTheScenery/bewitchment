plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/bewitchment.accesswidener")
}

repositories {
    maven {
        name "cardinal components, trinkets"
        url "https://maven.ladysnake.org/releases"
    }
    maven {
        name "impersonate, playerabilitylib"
        url "https://dl.bintray.com/ladysnake/libs"
    }
    maven {
        name "impersonate"
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
        name "reach entity attributes"
        url "https://maven.jamieswhiteshirt.com/libs-release"
    }
    maven {
        name "terraform, trinkets"
        url "https://maven.terraformersmc.com"
    }
    maven {
        name "patchouli"
        url "https://maven.blamejared.com"
    }
    maven {
        name "appleskin"
        url "https://maven.ryanliptak.com"
    }
    maven {
        name "rei"
        url "https://maven.shedaniel.me"
    }
    maven {
        name "modrinth"
        url "https://api.modrinth.com/maven"
    }
    maven {
        name "jitpack"
        url "https://jitpack.io"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cardinal_components_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cardinal_components_version}"
    //^ don't include because trinkets already does
    include "me.lucko:fabric-permissions-api:${fabric_permissions_api_version}"
    modImplementation "io.github.ladysnake:impersonate:${impersonate_version}"
    include "io.github.ladysnake:impersonate:${impersonate_version}"
    modImplementation "maven.modrinth:midnightlib:${midnightlib_version}"
    include "maven.modrinth:midnightlib:${midnightlib_version}"
    modImplementation "io.github.ladysnake:PlayerAbilityLib:${playerabilitylib_version}"
    include "io.github.ladysnake:PlayerAbilityLib:${playerabilitylib_version}"
    modImplementation "com.jamieswhiteshirt:reach-entity-attributes:${reach_entity_attributes_version}"
    include "com.jamieswhiteshirt:reach-entity-attributes:${reach_entity_attributes_version}"
    modImplementation "com.github.emilyploszaj:step-height-entity-attribute:${step_height_version}"
    include "com.github.emilyploszaj:step-height-entity-attribute:${step_height_version}"
    modImplementation "com.terraformersmc.terraform-api:terraform-wood-api-v1:${terraform_wood_api_version}"
    include "com.terraformersmc.terraform-api:terraform-wood-api-v1:${terraform_wood_api_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "vazkii.patchouli:Patchouli:${patchouli_version}"
    modImplementation("com.github.Virtuoel:Pehkui:${pehkui_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
    modImplementation "dev.emi:trinkets:${trinkets_version}"

    modCompileOnly("com.github.apace100:apoli:${apoli_version}") {
        exclude(group: "*")
    }
    modCompileOnly "squeek.appleskin:appleskin-fabric:${appleskin_version}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"

    modRuntimeOnly "maven.modrinth:modmenu:${mod_menu_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}
